Program.Sub.ScreenSU.Start
Gui.Form..create
Gui.Form..caption("Positive Pay - Capital One Bank")
Gui.Form..size(13470,10710)
Gui.Form..position(0,0)
Gui.Form..event(UnLoad,Form_UnLoad)
Gui.Form..forecolor(0)
Gui.Form..BackColor(-2147483633)
Gui.Form..mousepointer(0)
Gui.Form..MinX(0)
Gui.Form..MinY(0)
Gui.Form.dpDate.create(datepicker)
Gui.Form.dpDate.size(1935,285)
Gui.Form.dpDate.position(75,420)
Gui.Form.dpDate.defaultvalue("")
Gui.Form.cmdOK.create(button)
Gui.Form.cmdOK.caption("OK")
Gui.Form.cmdOK.size(855,375)
Gui.Form.cmdOK.position(60,1455)
Gui.Form.cmdOK.event(Click,cmdOK_Click)
Gui.Form.cmdOK.defaultvalue("")
Gui.Form.lbl1.Create(Label,"Select Date",True,1035,255,0,60,195,True,0,"Arial",8,-2147483633,0)
Gui.Form.txtExportFolder.Create(TextBox,"",True,5580,300,0,75,1065,True,0,"Arial",8,-2147483643,1)
Gui.Form.lbl2.Create(Label,"Export Folder",True,1215,255,0,60,855,True,0,"Arial",8,-2147483633,0)
Gui.Form.cmdBrowseExportFolder.Create(Button)
Gui.Form.cmdBrowseExportFolder.Size(570,375)
Gui.Form.cmdBrowseExportFolder.Position(5745,1020)
Gui.Form.cmdBrowseExportFolder.Caption("^")
Gui.Form.cmdBrowseExportFolder.Event(Click,cmdBrowseExportFolder_Click)
Gui.Form.txtBankAccount.Create(TextBox,"7047552952",True,1080,300,0,3390,405,True,1,"Arial",8,-2147483643,1)
Gui.Form.txtBankAccount.DefaultValue("")
Gui.Form.txtBankAccount.MaxLength(10)
Gui.Form.lbl4.Create(Label,"Bank Account",True,1275,255,0,3390,180,True,0,"Arial",8,-2147483633,0)
Gui.Form.gsCustom.Create(GsGridControl)
Gui.Form.gsCustom.Size(13185,7815)
Gui.Form.gsCustom.Position(75,1905)
Gui.Form.cmdRun.Create(Button)
Gui.Form.cmdRun.Enabled(False)
Gui.Form.cmdRun.Size(855,375)
Gui.Form.cmdRun.Position(45,9795)
Gui.Form.cmdRun.Caption("Run")
Gui.Form.cmdRun.DefaultValue("")
Gui.Form.cmdRun.Event(Click,cmdRun_Click)
Gui.Form.txtRouting.Create(TextBox,"021407912",True,1005,300,0,2175,405,True,1,"Arial",8,-2147483643,1)
Gui.Form.txtRouting.MaxLength(9)
Gui.Form.lbl5.Create(Label,"Routing",True,765,255,0,2175,180,True,0,"Arial",8,-2147483633,0)
Program.Sub.ScreenSU.End

Program.Sub.Preflight.Start
Program.Sub.Preflight.End

Program.Sub.Main.Start
'BN, July 24, 2019
'Customer: Hy-Tech
'This project will generate a csv positive pay for Capital One Bank

f.Intrinsic.Control.Try
	v.Local.sError.Declare
	v.Local.sDefaultfolder.Declare
	v.Local.bExist.Declare
	v.Local.sRet.Declare

	F.Odbc.Connection!Conx.OpenCompanyConnection
	
	'Check default folder
	f.Intrinsic.String.Build("{0}\Custom\5695",v.Caller.GlobalDir,v.Local.sDefaultfolder)
	f.Intrinsic.File.DirExists(v.Local.sDefaultfolder,v.Local.bExist)
	f.Intrinsic.Control.If(v.Local.bExist.Not)
		f.Intrinsic.File.CreateDir(v.Local.sDefaultfolder)
	f.Intrinsic.Control.EndIf	
	
	'Default Positive Pay folder that store the csv output file.
	f.Intrinsic.String.Build("{0}\Custom\5695\{1}",v.Caller.GlobalDir,v.Caller.CompanyCode,v.Local.sDefaultfolder)
	f.Intrinsic.File.DirExists(v.Local.sDefaultfolder,v.Local.bExist)
	f.Intrinsic.Control.If(v.Local.bExist.Not)
		f.Intrinsic.File.CreateDir(v.Local.sDefaultfolder)
	f.Intrinsic.Control.EndIf
	'Set Default folder
	gui.Form.txtExportFolder.Text(v.Local.sDefaultfolder)
	gui.Form.gsCustom.Anchor(15)
	gui.Form.cmdRun.Anchor(6)
	gui.Form..Show

F.Intrinsic.Control.Catch
	F.Intrinsic.String.Build("Project: {0}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}{1}GAB Version: {5}}",V.Ambient.ScriptPath,V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
	f.Intrinsic.Control.CallSub("Unload")
F.Intrinsic.Control.EndTry
Program.Sub.Main.End

Program.Sub.cmdOK_Click.Start
'Verify if there are checks for the selected date. If yes, show the grid for the user 
f.Intrinsic.Control.Try	
	V.Local.sError.Declare
	v.Local.sSQL.Declare
	v.Local.bExist.Declare
	V.Local.sDateSelected.Declare
	v.Local.iCnt.Declare
	v.Local.sHold.Declare	
	v.Local.iInner.Declare
	
	'Check Export Folder
	f.Intrinsic.File.DirExists(v.Screen.Form!txtExportFolder.Text.Trim,v.Local.bExist)
	f.Intrinsic.Control.If(v.Local.bExist.Not)
		f.Intrinsic.UI.Msgbox("Export Folder does not exist")
		f.Intrinsic.Control.ExitSub
	f.Intrinsic.Control.EndIf	
	
	f.Intrinsic.UI.InvokeWaitDialog("Processing...")
	gui.Form..Visible(false)
	
	'format date
	F.Intrinsic.String.Format(V.Screen.Form!dpDate.Value,"YYYY-MM-DD",V.Local.sDateSelected)	
	
	'select data for correct date and account number
	F.intrinsic.string.Build("select Ck_Date,Ck_Or_Void,ltrim(rtrim(Ck_no)) as CK_No,Payee, Amount, Vend_No from V_CHECKS_LOG where CK_DATE= '{0}' order by CK_NO",v.Local.sDateSelected,V.Local.sSQL)
	f.Intrinsic.Control.If(v.DataTable.PreT.Exists)
		f.Data.DataTable.Close("PreT")
	f.Intrinsic.Control.EndIf
	f.Data.DataTable.CreateFromSQL("PreT","conx",v.Local.sSQL,True)	
	f.Intrinsic.Control.If(v.DataTable.PreT.RowCount,=,0)
		f.Intrinsic.UI.Msgbox("No checks to export")	
		f.Intrinsic.UI.CloseWaitDialog
		gui.Form..Show
		f.Intrinsic.Control.ExitSub
	f.Intrinsic.Control.EndIf	

	f.Data.DataTable.AddColumn("PreT","Exclude","Boolean",False)
	
	f.Intrinsic.UI.CloseWaitDialog
	
	Gui.Form.gsCustom.AddGridviewFromDataTable("GVMain","PreT")

	'General format
	Gui.Form.gsCustom.SetGridViewProperty("GVMain","AllowSort",True)
	Gui.Form.gsCustom.SetGridViewProperty("GVMain","AllowFilter",True)
	Gui.Form.gsCustom.SetGridViewProperty("GVMain","OptionsFindAlwaysVisible",True)
	Gui.Form.gsCustom.SetGridViewProperty("GVMain","OptionsViewShowAutoFilterRow",True)
	Gui.Form.gsCustom.SetGridViewProperty("GVMain","OptionsDetailShowDetailTabs",True)
	Gui.Form.gsCustom.SetGridViewProperty("GVMain","OptionsViewShowGroupPanel",True)
	
	'Column format	
	gui.Form.gsCustom.SetColumnProperty("GVMain","Exclude","AllowEdit",True)
	gui.Form.gsCustom.SetColumnProperty("GVMain","Exclude","ReadOnly",False)
	'Width
	gui.Form.gsCustom.SetColumnProperty("GVMain","Vend_No","Width","40")
	gui.Form.gsCustom.SetColumnProperty("GVMain","CK_Or_Void","Width","65")
	gui.Form.gsCustom.SetColumnProperty("GVMain","Exclude","Width","50")
	gui.Form.gsCustom.SetColumnProperty("GVMain","CK_Date","Width","65")
	gui.Form.gsCustom.SetColumnProperty("GVMain","Amount","Width","55")
	'Header
	gui.Form.gsCustom.SetColumnProperty("GVMain","Exclude","HeaderHAlignment","Center")
	gui.Form.gsCustom.SetColumnProperty("GVMain","Amount","HeaderHAlignment","far")
	'Header Font Bold
	gui.Form.gsCustom.SetColumnProperty("GVMain","Ck_Date","HeaderFontBold",True)
	gui.Form.gsCustom.SetColumnProperty("GVMain","Ck_No","HeaderFontBold",True)
	gui.Form.gsCustom.SetColumnProperty("GVMain","Amount","HeaderFontBold",True)
	gui.Form.gsCustom.SetColumnProperty("GVMain","Vend_No","HeaderFontBold",True)
	gui.Form.gsCustom.SetColumnProperty("GVMain","Exclude","HeaderFontBold",True)
	gui.Form.gsCustom.SetColumnProperty("GVMain","Payee","HeaderFontBold",True)
	gui.Form.gsCustom.SetColumnProperty("GVMain","CK_Or_Void","HeaderFontBold",True)
	'Caption
	gui.Form.gsCustom.SetColumnProperty("GVMain","Vend_No","Caption","Vendor")
	gui.Form.gsCustom.SetColumnProperty("GVMain","CK_Or_Void","Caption","Check/Void")
	gui.Form.gsCustom.SetColumnProperty("GVMain","CK_Date","Caption","Check Date")
	gui.Form.gsCustom.SetColumnProperty("GVMain","CK_No","Caption","Check Number")	
	'Numeric
	gui.Form.gsCustom.SetColumnProperty("GVMain","Amount","DisplayCustomNumeric","###,###,###,##0.00")	
	'Custom date
	Gui.Form.gsCustom.SetColumnProperty("GVMain","CK_Date","DisplayCustomDatetime","d")		
	
	gui.Form.gsCustom.MainView("GVMain")
	gui.Form..Show
	'Deactivate Controls
	gui.Form.dpDate.Enabled(False)
'	gui.Form.txtCourcode.Enabled(False)
	gui.Form.txtBankAccount.Enabled(False)
	gui.Form.txtExportFolder.Enabled(False)
	gui.Form.cmdBrowseExportFolder.Enabled(False)
	gui.Form.cmdOK.Enabled(False)
	'Activate run button
	gui.Form.cmdRun.Enabled(True)

	f.Intrinsic.Control.ExitSub
	
F.Intrinsic.Control.Catch
	F.Intrinsic.String.Build("Project: {0}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}{1}GAB Version: {5}}",V.Ambient.ScriptPath,V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
	f.Intrinsic.Control.CallSub("Unload")
F.Intrinsic.Control.EndTry
Program.Sub.cmdOK_Click.End

Program.Sub.cmdRun_Click.Start
f.Intrinsic.Control.Try	
	'string
	V.Local.sError.Declare
	V.local.sExport.declare
	v.Local.sFile.Declare
	v.Local.sMsg.Declare
	V.Local.sDateSelected.Declare
	v.Local.sDateSubmitted.Declare
	v.Local.iCnt.Declare
	v.Local.sHold.Declare
	v.Local.sDateSubmittedWithDash.Declare	
	v.Local.iMonth.Declare
	v.Local.iDay.Declare
	v.Local.iYear.Declare
	
	v.Local.sRouting.Declare
	v.Local.sAccount.Declare
	v.Local.sCheckNumber.Declare
	v.Local.sAmount.Declare
	v.Local.sIssueDate.Declare
	v.Local.dIssueDate.Declare
	v.Local.sVoid.Declare
	v.Local.sPayeeName.Declare
	v.Local.sContent.Declare
	
	f.Intrinsic.UI.InvokeWaitDialog("Processing...")
	gui.Form..Visible(false)
	
	'format date
	F.Intrinsic.String.Format(V.Screen.Form!dpDate.Value,"YYYY-MM-DD",V.Local.sDateSelected)	
	F.Intrinsic.String.Format(v.Ambient.Date,"MMDDYYYY",V.Local.sDateSubmitted)	
	f.Intrinsic.String.Mid(v.Local.sDateSubmitted,3,2,v.Local.sHold)
	v.Local.iMonth.Set(v.Local.sDateSubmitted.left2)
	v.Local.iDay.Set(v.Local.sHold)
	v.Local.iYear.Set(v.Local.sDateSubmitted.Right4)
	f.Intrinsic.String.Build("{0}/{1}/{2}",v.Local.iMonth,v.Local.iDay,v.Local.iYear,v.Local.sDateSubmittedWithDash)
	f.Intrinsic.String.Build("{0}\{1}_Select_{2}_Submit_{3}_{4}.csv",v.Screen.Form!txtExportFolder.Text.Trim,v.Caller.CompanyCode,v.Local.sDateSelected,v.Ambient.Date.FormatYYYYMMDD,v.Ambient.Now.FormatHhNnSs,v.Local.sFile)		
	
	f.Data.DataView.Create("PreT","PreTDV",22,"Exclude = 0","")
	
	'1/Routing
	f.Intrinsic.String.LPad(v.Screen.Form!txtRouting.Text.Trim,"0",9,v.Local.sRouting)
	'2/Bank Account
	f.Intrinsic.String.LPad(v.Screen.Form!txtBankAccount.Text.Trim,"0",10,v.Local.sAccount)
	
	f.Intrinsic.Control.For(v.Local.iCnt,0,v.DataView.PreT!PreTDV.RowCount--,1)
		'3/Check number
		f.Intrinsic.String.LPad(v.DataView.PreT!PreTDV(v.Local.iCnt).Ck_No!FieldValString,"0",10,v.Local.sCheckNumber)
		'4/Check Amount
		f.Intrinsic.String.Format(v.DataView.PreT!PreTDV(v.Local.iCnt).Amount!FieldValString,"#.00",v.Local.sAmount)	
		f.Intrinsic.String.Replace(v.Local.sAmount,".","",v.Local.sAmount)
		f.Intrinsic.String.LPad(v.Local.sAmount,"0",10,v.Local.sAmount)
		'5/Issue date
		v.Local.dIssueDate.Set(v.DataView.PreT!PreTDV(v.Local.iCnt).Ck_Date!FieldValString)
		f.Intrinsic.String.Format(v.Local.dIssueDate,"MMDDYY",v.Local.sIssueDate)
		'6/Void
		f.Intrinsic.Control.If(v.DataView.PreT!PreTDV(v.Local.iCnt).Ck_Or_Void!FieldValTrim,=,"V")
			v.Local.sVoid.Set("V")
		f.Intrinsic.Control.Else
			v.Local.sVoid.Set("")
		f.Intrinsic.Control.EndIf
		'7/Payee Name
		f.Intrinsic.String.RPad(v.DataView.PreT!PreTDV(v.Local.iCnt).Payee!FieldValString," ",80,v.Local.sPayeeName)
		
		f.Intrinsic.Control.If(v.Local.iCnt,=,0)
			f.Intrinsic.String.Build("{0},{1},{2},{3},{4},{5},{6}",v.Local.sRouting,v.Local.sAccount,v.Local.sCheckNumber,v.Local.sAmount,v.Local.sIssueDate,v.Local.sVoid,v.Local.sPayeeName,v.Local.sContent)
		f.Intrinsic.Control.Else
			f.Intrinsic.String.Build("{0}{1}{2},{3},{4},{5},{6},{7},{8}",v.Local.sContent,v.Ambient.NewLine,v.Local.sRouting,v.Local.sAccount,v.Local.sCheckNumber,v.Local.sAmount,v.Local.sIssueDate,v.Local.sVoid,v.Local.sPayeeName,v.Local.sContent)
		f.Intrinsic.Control.EndIf			

	f.Intrinsic.Control.Next(v.Local.iCnt)	

	'Write file
	f.Intrinsic.File.String2File(v.Local.sFile,v.Local.sContent)
	
	'Msg Done and unload
	f.Intrinsic.UI.CloseWaitDialog	
	f.Intrinsic.String.Build("Generated file {0}",v.Local.sFile,v.Local.sMsg)
	f.Intrinsic.UI.Msgbox(v.Local.sMsg,"Done")
	F.Intrinsic.Control.CallSub("Unload")

F.Intrinsic.Control.Catch
	F.Intrinsic.String.Build("Project: {0}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}{1}GAB Version: {5}}",V.Ambient.ScriptPath,V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
	f.Intrinsic.Control.CallSub("Unload")
F.Intrinsic.Control.EndTry

Program.Sub.cmdRun_Click.End

Program.Sub.Form_UnLoad.Start
f.Intrinsic.Control.CallSub("Unload")
Program.Sub.Form_UnLoad.End

Program.Sub.UnLoad.Start
f.Intrinsic.Control.Try
	v.Local.sError.Declare

	F.Intrinsic.Control.If(V.ODBC.conx.State,=,1)
		F.ODBC.Connection!conx.close
	F.Intrinsic.Control.EndIf
	
	f.Intrinsic.Control.End

F.Intrinsic.Control.Catch
	f.Intrinsic.String.Build("Subroutine Error {0}{0}Subroutine: {1}{0}Error Occurred {2} with description {3}",V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
	F.Intrinsic.Control.End	
F.Intrinsic.Control.EndTry
Program.Sub.UnLoad.End

Program.Sub.cmdBrowseExportFolder_Click.Start
'Browse Export Folder
f.Intrinsic.Control.Try
	v.Local.sError.Declare
	v.Local.sPath.Declare
	
	F.Intrinsic.UI.FolderBrowser("Select Folder for Path",V.Local.sPath)
	f.Intrinsic.Control.If(v.Local.sPath.IsCancel,=,False)
		gui.Form.txtExportFolder.Text(v.Local.sPath)
	f.Intrinsic.Control.EndIf

F.Intrinsic.Control.Catch
	f.Intrinsic.String.Build("Subroutine Error {0}{0}Subroutine: {1}{0}Error Occurred {2} with description {3}",V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
	F.Intrinsic.Control.End	
F.Intrinsic.Control.EndTry
Program.Sub.cmdBrowseExportFolder_Click.End

Program.Sub.Comments.Start
${$5$}$2.0.0.0$}$2
${$6$}$Angel$}$20220324150920994$}$KXl0r/FkrMYwb/r4ukQ87aOTsTN7X2eavVbntoSQczhn/WoqcPDH5Yb2LyGlj5oeLC01y6F2XsU=
Program.Sub.Comments.End
